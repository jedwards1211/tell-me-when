YearMonthDay<sep, month> {
  Year { FullYear ~d }
  (
    sep
    Month { month ~YearMonthDay }
    (
      sep
      Day { NumericDayOfMonth | DayOfMonthOrdinal }
    )?
  )?
}

MonthNameDayYear<sep1, sep2> {
  Month { MonthName ~m }
  (
    sep1?
    Day { NumericDayOfMonth | DayOfMonthOrdinal }
    (
      sep2?
      Year { FullYear | TwoDigitYear }
    )?
  )?
}
MonthNameMaybeDotDayYear<sep1, sep2> {
  Month { MonthNameMaybeDot ~m }
  (
    sep1?
    Day { NumericDayOfMonth | DayOfMonthOrdinal }
    (
      sep2?
      Year { FullYear | TwoDigitYear }
    )?
  )?
}
MonthNumberDayYear<sep> {
  Month { MonthNumber ~d }
  (
    sep
    Day { NumericDayOfMonth | DayOfMonthOrdinal }
    (
      sep
      Year { FullYear | TwoDigitYear }
    )?
  )?
}

DayMonthYear<sep, month> {
  Day { DayOfMonthOrdinal ~day | NumericDayOfMonth ~d }
  (
    sep
    Month { month ~DayMonthYear }
    (
      sep
      Year { FullYear | TwoDigitYear }
    )?
  )
}

DayOfMonthYear {
  Day { DayOfMonthOrdinal ~day }
  (
    space ('day' space)? 'of' space
    (
      Month { MonthName }
      (
        (space? ',' space? | space)
        Year { FullYear | TwoDigitYear }
      )?
    )
  )?
}

@top DateTimeExpression {
  YearMonthDay<'', MonthNameMaybeDot>
  | YearMonthDay<'.', MonthName>
  | YearMonthDay<'-', MonthNameMaybeDot>
  | YearMonthDay<'_', MonthNameMaybeDot>
  | YearMonthDay<'/', MonthNameMaybeDot>
  | YearMonthDay<space, MonthNameMaybeDot>
  | YearMonthDay<'.', MonthNumber>
  | YearMonthDay<'-', MonthNumber>
  | YearMonthDay<'_', MonthNumber>
  | YearMonthDay<'/', MonthNumber>
  | YearMonthDay<space, MonthNumber>
  | MonthNameDayYear<'.', '.'>
  | MonthNameMaybeDotDayYear<'-', '-'>
  | MonthNameMaybeDotDayYear<'_', '_'>
  | MonthNameMaybeDotDayYear<'/', '/'>
  | MonthNameMaybeDotDayYear<space, space | (space? ',' space?)>
  | MonthNumberDayYear<'.'>
  | MonthNumberDayYear<'-'>
  | MonthNumberDayYear<'_'>
  | MonthNumberDayYear<'/'>
  | MonthNumberDayYear<space>
  | DayOfMonthYear
  | DayMonthYear<'', MonthNameMaybeDot>
  | DayMonthYear<'.', MonthName>
  | DayMonthYear<'-', MonthNameMaybeDot>
  | DayMonthYear<'_', MonthNameMaybeDot>
  | DayMonthYear<'/', MonthNameMaybeDot>
  | DayMonthYear<space, MonthNameMaybeDot>
  | DayMonthYear<'.', MonthNumber>
  | DayMonthYear<'-', MonthNumber>
  | DayMonthYear<'_', MonthNumber>
  | DayMonthYear<'/', MonthNumber>
  | DayMonthYear<space, MonthNumber>
}

MonthNumber {
  (
    '1'
    | '2'
    | '3'
    | '4'
    | '5'
    | '6'
    | '7'
    | '8'
    | '9'
    | '01'
    | '02'
    | '03'
    | '04'
    | '05'
    | '06'
    | '07'
    | '08'
    | '09'
    | '10'
    | '11'
    | '12'    
  ) ~n
}
NumericDayOfMonth {
  (
    '1'
    | '2'
    | '3'
    | '4'
    | '5'
    | '6'
    | '7'
    | '8'
    | '9'
    | '01'
    | '02'
    | '03'
    | '04'
    | '05'
    | '06'
    | '07'
    | '08'
    | '09'
    | '10'
    | '11'
    | '12'
    | '13'
    | '14'
    | '15'
    | '16'
    | '17'
    | '18'
    | '19'   
    | '20'
    | '21'
    | '22'
    | '23'
    | '24'
    | '25'
    | '26'
    | '27'
    | '28'
    | '29'   
    | '30'
    | '31'
  ) ~n
}

MonthNameFullExceptMay {
  'january'
  | 'february'
  | 'march'
  | 'april'
  | 'june'
  | 'july'
  | 'august'
  | 'september'
  | 'october'
  | 'november'
  | 'december' 
}

MonthNameAbbrev {
  'jan'
  | 'feb'
  | 'mar'
  | 'apr'
  | 'may'
  | 'jun'
  | 'jul'
  | 'aug'
  | ('sep' 't'?)
  | 'oct'
  | 'nov'
  | 'dec'
}

MonthName {
  MonthNameFullExceptMay
  | MonthNameAbbrev
}

MonthNameMaybeDot {
  (MonthNameAbbrev ~m '.'?)
  | MonthNameFullExceptMay
}

@tokens {
  space { @whitespace+ }

  FullYear { @digit @digit @digit @digit }
  TwoDigitYear { "'"? @digit @digit }
  DayOfMonthOrdinal {
    '1st' | 'first'
    | '2nd' | 'second'
    | '3rd' | 'third'
    | '4th' | 'fourth'
    | '5th' | 'fifth'
    | '6th' | 'sixth'
    | '7th' | 'seventh'
    | '8th' | 'eighth'
    | '9th' | 'ninth'
    | '10th' | 'tenth'
    | '11th' | 'eleventh'
    | '12th' | 'twelfth'
    | '13th' | 'thirteenth'
    | '14th' | 'fourteenth'
    | '15th' | 'fifteenth'
    | '16th' | 'sixteenth'
    | '17th' | 'seventeenth'
    | '18th' | 'eighteenth'
    | '19th' | 'ninteenth'  
    | '20th' | 'twentieth'
    | '21st' | 'twenty-first'
    | '22nd' | 'twenty-second'
    | '23rd' | 'twenty-third'
    | '24th' | 'twenty-fourth'
    | '25th' | 'twenty-fifth'
    | '26th' | 'twenty-sixth'
    | '27th' | 'twenty-seventh'
    | '28th' | 'twenty-eighth'
    | '29th' | 'twenty-ninthy'
    | '30th' | 'thirtieth'
    | '31st' | 'thirty-first'
  }
}
